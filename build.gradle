buildscript {
    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '0.4.8'
}

def ideaVersionExternalDefinition = System.getenv("IDEA_VERSION")
ideaVersion = ideaVersionExternalDefinition ? ideaVersionExternalDefinition : ideaVersion
def isEAP = ideaVersion.contains("LATEST-EAP-SNAPSHOT") || ideaVersion.take(4).contains(".")
def artifactVersion = pluginVersion + "-" + (isEAP ? "EAP" : ideaVersion)
println "IdeaVersion is : ${ideaVersion}"
println "ArtifactVersion is : ${artifactVersion}"

intellij {
    version ideaVersion
    println "Building for IntelliJ version: ${version}"

    pluginName pluginName
    updateSinceUntilBuild true
    sameSinceUntilBuild false

    plugins 'coverage'
}

group pluginGroup
version pluginVersion

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

patchPluginXml {
    version artifactVersion

    if (isEAP || (ideaVersion == lastReleaseVersion)) {
        untilBuild ""
    }

    pluginDescription(file(descriptionFile).text)
    changeNotes(file(changesFile).text)
}

publishPlugin {
    username System.getenv('IJ_REPO_USERNAME')
    password System.getenv('IJ_REPO_PASSWORD')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}